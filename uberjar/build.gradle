// The various tasks that jenkins needs to do are split up by project, with each in their own
// gradle file.

// We expect the version of ntcore and wpiutil be somewhere in the products directory
def finder = new FileNameFinder()
def utilVersion = file(finder.getFileNames(new File(projectDir, 'products').absolutePath, '**/wpiutil.txt').first()).text.trim()
def ntcoreVersion = file(finder.getFileNames(new File(projectDir, 'products').absolutePath, '**/ntcore.txt').first()).text.trim()

apply plugin: ProductCombinerPlugin

products {
    ntcoreJava {
        group = 'edu.wpi.first.wpilib.networktables.java'
        productName = 'NetworkTables'
        versionNum = ntcoreVersion
        fileNameInclude = 'ntcore'
        excludes = ['**/*javadoc*', '**/*sources*', '**/*arm*']
        armInclude = '**/ntcore-arm*.jar'
        sourceInclude = '**/ntcore-source*.jar'
        docInclude = '**/ntcore-javadoc*.jar'
        docClassifier = 'javadoc'
        type = Type.Jar
    }
    ntcoreCpp {
        group = 'edu.wpi.first.wpilib.networktables.cpp'
        productName = 'NetworkTables'
        versionNum = ntcoreVersion
        fileNameInclude = 'ntcore'
        excludes = ['**/*javadoc*', '**/*sources*', '**/*arm*']
        armInclude = '**/ntcore-arm*.zip'
        sourceInclude = '**/ntcore-source*.zip'
        includeDocs = false
        type = Type.Zip
    }
    wpiutil {
        group = 'edu.wpi.first.wpilib'
        productName = 'wpiutil'
        versionNum = utilVersion
        fileNameInclude = 'wpiutil'
        excludes = ['**/*javadoc*', '**/*sources*', '**/*arm*']
        armInclude = '**/wpiutil-arm*.zip'
        sourceInclude = '**/wpiutil-source*.zip'
        includeDocs = false
        type = Type.Zip
    }
}

task clean(type: Delete) {
    delete project.buildDir
}

class ProductCombinerPlugin implements Plugin<Project> {
    void apply(Project project) {
        // Apply and set up the maven-publish plugin
        project.apply plugin: 'maven-publish'
        if (!project.hasProperty('repo')) {
            project.extensions.repo = 'development'
        }

        project.publishing {
            repositories {
                maven {
                    url "${System.getProperty('user.home')}/releases/maven/${project.repo}"
                }
            }
        }

        def artifacts = project.container(Product)
        project.afterEvaluate {
            project.products.all {
                def isJar = type == Type.Jar
                def dir = 'products'

                if (sources == null) {
                    def include = "**/*$fileNameInclude*${isJar ? '.jar' : '.zip'}"
                    excludes << "${project.buildDir}/**/*"
                    sources = project.fileTree(dir: dir, include: include, exclude: excludes)
                }

                if (armFile == null) {
                    armFile = getFile(dir, armInclude, project)
                }

                if (docFile == null && includeDocs) {
                    docFile = getFile(dir, docInclude, project)
                }

                if (sourceFile == null) {
                    sourceFile = getFile(dir, sourceInclude, project)
                }

                def mainArt = project.task("${name}${isJar ? 'Jar' : 'Zip'}", type: isJar ? Jar : Zip) {
                    description = "Creates the $name."
                    group = productName
                    baseName = productName
                    classifier = mainClassifier
                    destinationDir = project.buildDir
                    duplicatesStrategy = 'exclude'

                    sources.each { from project.zipTree(it) }
                }

                project.publishing {
                    publications {
                        "${name}"(MavenPublication) {
                            artifact(mainArt) {
                                classifier = mainClassifier
                            }

                            artifact(armFile) {
                                classifier = armClassifier
                            }

                            artifact(sourceFile) {
                                classifier = sourceClassifier
                            }

                            if (includeDocs) {
                                artifact(docFile) {
                                    classifier = docClassifier
                                }
                            }

                            groupId = group
                            artifactId = productName
                            version = versionNum
                        }
                    }
                }
            }
        }
        project.extensions.products = artifacts
    }

    File getFile(dir, includes, project) {
        def file = project.fileTree(dir: dir, include: includes).files[0]
        if (file == null) {
            throw new Exception("Could not find $includes in $dir")
        }
        return file
    }

}

class Product {
    final String name
    String group
    String productName
    String versionNum
    String mainClassifier = 'desktop'
    String fileNameInclude
    List<String> excludes = []
    FileTree sources
    String sourceInclude
    File sourceFile
    String sourceClassifier = 'sources'
    boolean includeDocs
    String docInclude
    File docFile
    String docClassifier
    String armInclude
    File armFile
    String armClassifier = 'arm'
    Type type

    Product(String name) {
        this.name = name
    }
}

enum Type {
    Jar, Zip
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}